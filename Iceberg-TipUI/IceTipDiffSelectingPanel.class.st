"
I'm a diff panel that shows packages/classes/methods changed in a column and a diff in the other.
I give users the possibility of select changes.  

By default, all my elements are selected on opening.

As my father, I receive a model that needs to understand #elements.
also #elements answer should be a collection of elements that also understand elements.


"
Class {
	#name : #IceTipDiffSelectingPanel,
	#superclass : #IceTipDiffPanel,
	#instVars : [
		'selectedItems'
	],
	#category : #'Iceberg-TipUI-View'
}

{ #category : #initialization }
IceTipDiffSelectingPanel >> changeListColumn [
	^ SpCompositeTableColumn new
		addColumn:
			(SpCheckBoxTableColumn new
				width: 20;
				evaluated: [ :item | selectedItems includes: item ];
				onActivation: [ :item | 
					selectedItems add: item.
					item children isEmptyOrNil
						ifFalse: [ item allChildrenDo: [ :child | selectedItems add: child ].
							self refreshCheckbox ] ];
				onDesactivation: [ :item | 
					selectedItems remove: item.
					item children isEmptyOrNil
						ifFalse: [ item
								allChildrenDo: [ :child | selectedItems remove: child ifAbsent: [  ] ].
							self refreshCheckbox. ] ]);
		addColumn:
			(SpImageTableColumn new
				width: 20;
				evaluated: [ :each | each value icon ];
				yourself);
		addColumn:
			(SpStringTableColumn new
				evaluated: [ :each | each definition name ];
				yourself);
		yourself
]

{ #category : #'as yet unclassified' }
IceTipDiffSelectingPanel >> collapseAll [

	changeList widget dataSource 
		rootForItems: self model treeRoots;
		selectAll
]

{ #category : #private }
IceTipDiffSelectingPanel >> entryRemoved: ann [
	self refresh
]

{ #category : #'as yet unclassified' }
IceTipDiffSelectingPanel >> expandAll [

	changeList widget dataSource 
		rootForItems: self model treeRoots;
		expandAll;
		selectAll
]

{ #category : #initialization }
IceTipDiffSelectingPanel >> initialize [
	super initialize.
	self subscribeToAnnouncements.
	selectedItems := Set new.
	self model treeRoots
		do: [ :root | 
			selectedItems add: root.
			root allChildrenDo: [ :child | selectedItems add: child.]]
]

{ #category : #'private factory' }
IceTipDiffSelectingPanel >> newChangeTreeDataSource [

	^ IceTipTreeSelectingDataSource new
		tool: self;
		rootForItems: self model treeRoots;
		childrenBlock: [ :data | data children ];
		icon: [ :each | each value icon ];
		format: #description;
		selectAll;
		yourself
]

{ #category : #accessing }
IceTipDiffSelectingPanel >> refresh [
	super refresh. self model treeRoots
		do: [ :root | 
			selectedItems add: root.
			root allChildrenDo: [ :child | selectedItems add: child ] ]
]

{ #category : #'as yet unclassified' }
IceTipDiffSelectingPanel >> refreshCheckbox [
	 self basicRefresh
]

{ #category : #private }
IceTipDiffSelectingPanel >> resetDataSource: aDataSource [
	super resetDataSource: aDataSource.
	aDataSource selectAll
]

{ #category : #accessing }
IceTipDiffSelectingPanel >> selectedItems [
	^selectedItems
]

{ #category : #initialize }
IceTipDiffSelectingPanel >> subscribeToAnnouncements [
	self announcer
		when: IceTipDiffEntryRemoved 
		send: #entryRemoved:
		to: self	
]
